language StudyConfigurationModel

concept EventReference {
    reference event: Event;
    eventState: EventState;
}

interface EventStart {
    operator?: SimpleOperators;
    timeAmount?: TimeAmount;
}  

concept When implements EventStart {
    startWhen: EventReference;
}

concept Day implements EventStart {
    startDay: number;
}

concept AnyDay implements EventStart {
}

concept StudyStart implements EventStart {
}

concept Previous implements EventStart {
}

interface RepeatExpression {}

//limited RepeatUnit implements RepeatExpression {
//limited RepeatUnit {
//    daily; weekly; monthly; forever;
//}

interface RepeatUnit {}

concept Daily implements RepeatUnit {}

concept Weekly implements RepeatUnit {}

concept Monthly implements RepeatUnit {}

concept Forever implements RepeatUnit {}

concept RepeatEvery implements RepeatUnit {
    repeatEvery: Time;
}

limited TimeUnit {
   hours; days; weeks; months;
}

limited EventState {
    completed; started; scheduled; skipped;
}

concept RepeatCondition implements RepeatExpression {
    repeatUnit: RepeatUnit;
    maxRepeats: number;
    until?: ScheduleExpression;
}

concept TimeAmount   {
    value: number;
    unit: TimeUnit = TimeUnit:days;
}

limited SimpleOperators {
    plus = { name:"+"} minus = { name: "-"}
}

concept RepeatCount implements RepeatExpression {
    repeatCount: number;
}

concept Days {
    count: number;
}

concept EventWindow {
    maxDaysBefore: Days;
    daysBefore: Days;
    daysAfter: Days;
    maxDaysAfter: Days;
}

concept EventTimeOfDay {
    startHour: number;
    startMinute: number;
    endHour: number;
    endMinute: number;
}

concept EventSchedule {
    eventStart?: EventStart;
    eventWindow: EventWindow;
    eventRepeat?: RepeatExpression;
    eventTimeOfDay?: EventTimeOfDay;
}
